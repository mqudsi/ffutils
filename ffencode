#!/usr/bin/env fish
# vim: set ft=fish:

# example: ffencode in.mp4 out.mp4

set FFMPEG ffmpeg
# set FFMPEG /mnt/d/rand/FFmpeg/ffmpeg_g
# set FFMPEG /mnt/c/Users/mqudsi/Downloads/ffmpeg-git-20191228-amd64-static/ffmpeg

if not test $FFMPEG = "ffmpeg" -o -x $FFMPEG
    echo "Cannot find ffmpeg at $FFMPEG"
    exit 1
end

function has_audio
    ffprobe -select_streams a:0 -of default=noprint_wrappers=1:nokey=1 -show_entries stream=codec_name -v quiet $argv[1] | string match -qr "."
end

function ts_to_secs
    # Split on : and treat as base 60 numbers to convert to seconds
    set parts (string split ':' -- $argv[1])
    set base 1
    set result 0
    for part in $parts[-1..1]
        set result (math "$result + $base * $part")
        set base (math "$base * 60")
    end

    echo $result
end

function secs_to_ts
    set -l duration $argv[1]
    set -l hh 0
    set -l ss $duration
    if test $duration -le 60
        set hh (math -s0 $duration / 60)
        set ss (math $duration % 60)
    end

    echo $hh:$ss
end

function ts_add
    set i 1
    set seconds
    for ts in $argv
        set seconds[$i] (ts_to_secs $ts)
        set i (math $i + 1)
    end

    # The duration in seconds (fractional OK)
    set duration $seconds[1]
    if set -q seconds[2]
        set duration (math $seconds[1] + $seconds[2])
    end

    # Convert it to a timestamp?
    secs_to_ts $duration
end

function ts_diff
    set i 1
    set seconds
    for ts in $argv
        set seconds[$i] (ts_to_secs $ts)
        set i (math $i + 1)
    end

    # The duration in seconds (fractional OK)
    set duration $seconds[1]
    if set -q seconds[2]
        set duration (math $seconds[1] - $seconds[2])
    end

    # Convert it to a timestamp?
    secs_to_ts $duration
end

# create a directory to hold build scripts and intermediate files
set outdir (mktemp -d)

set filters

set corecount (grep -P '^core id\t' /proc/cpuinfo | wc -l)
# set threads (math "$corecount * 0.50")
set threads (math -s0 "$corecount")
set threads 16

# Some default values pertaining to video encoding
set crf 18
set codec libx265
set codec_extra
set range
set audio "-c:a copy"
set preset "medium"

set source (realpath $argv[-2])
set dest (realpath $argv[-1])

if not test -e $source
    echo \"$source\" not found! 1>&2
    exit 1
end


set i 0
while test (set i (math $i + 1); echo $i) -le (count $argv)
    set arg $argv[$i]
    echo arg: $arg

    if test $arg = "--aom" || test $arg = "--av1"
        set codec "av1"
        set codec_extra $codec_extra -strict -2
    end

    if test $arg = "--x264"
        set codec "libx264"
    end

    if test $arg = "--range"
        set i (math $i + 1); set arg $argv[$i]
        if not string match -qr , -- $arg
            echo "Invalid range value! Expected: hh1:ss1,hh2:ss2" 1>&2
            exit -1
        end
        set range (string split , -- $arg)
        if not string match -qr "." -- $range[1]
            set range[1] 0
        end
    end

    if test $arg = "--crf"
        set i (math $i + 1); set arg $argv[$i]
        set crf $arg
    end

    if test $arg = "--decomb"
        # set filters $filters yadif
        set filters $filters bwdif
    end

    if test $arg = "--stabilize"
        command $FFMPEG -i $source -vf vidstabdetect=stepsize=4:shakiness=9:accuracy=15:result=$outdir/transform_vectors.trf -f null -
        set filters $filters vidstabtransform=input=$outdir/transform_vectors.trf:zoom=1:smoothing=30,unsharp=5:5:0.8:3:3:0.4

        set arg --crop
    end

    if test $arg = "--audio"
        set audio "-c:a libfdk_aac -ac 1 -b:a 48k"
    end

    if test $arg = "--filter"
        set i (math $i + 1); set arg $argv[$i]
        set filters $filters $arg
    end

    if test $arg = "--veryslow"
        set preset "veryslow"
    end

    if test $arg = "--crop"
        echo "Detecting crop..."
        # See https://trac.ffmpeg.org/ticket/6375 for `-max_muxing_queue_size 99999` reason
        set crop (command $FFMPEG -i $source -max_muxing_queue_size 99999 -vf cropdetect -f null - 2>|
                    string match -r 'crop=.*' | sort | uniq -c | sort -nb | tail -n 1 | string match -r 'crop=.*')
        echo "Crop detected: " $crop
        set filters $filters $crop
        sleep 1
    end

    if test $arg = "--single"
        set threads 1
    end
end

set build

set old_codec (ffidentify $source)
set old_codec_video (string replace -rf -- "video: " "" $old_codec)
set old_codec_audio (string replace -rf -- "audio: " "" $old_codec)

# re-encode unsupported audio formats in mp4 containers
if string match -q $old_codec_audio pcm_s16le
    set audio "-c:a libfdk_aac -ac 1 -b:a 48k"
end

# set outdir /tmp/tmp.aJ6ntpV6qY/
set script $outdir/build.ninja
rm -f $script


set src_bitrate (ffprobe -v quiet -select_streams v:0 -show_entries stream=bit_rate -of default=noprint_wrappers=1:nokey=1 $source)
if string match -q "N/A" $src_bitrate
    # mkv and some other container formats do not store the per-stream duration or the bitrate
    set src_bitrate (ffprobe -v quiet -show_entries format=bit_rate -of default=noprint_wrappers=1:nokey=1 $source)
end

echo "Source bitrate: $src_bitrate"
if test $codec = "libx265"
    # yuvj420p preserves full-range color values (0-255) rather than yuv420's limited range (16-235),
    # but the output is strictly 8-bit. There is no yuvj420p10le/yuvj420p12le for 10/12-bit output.
    # set codec_extra $codec_extra -pix_fmt yuvj420p
    set codec_extra $codec_extra -pix_fmt yuv420p12le

    # Set pass-through x265 options
    set x265_params vbv-bufsize=1000

    if set max_bitrate (math -s0 "0.8 * $src_bitrate / 1000")
        # Give x265 a guesstimate on resulting size
        # vbv-maxrate is in kilobits
        set x265_params $x265_params:vbv-maxrate=$max_bitrate
    end

    # -- Experimental feature to enable adaptive quantization Ã  la x264 --
    # It seems buggy: for files of similar characteristics most have output sizes comparable
    # to without this feature enabled (as expected), but others are almost double in size, at
    # least as of x265-3.1+2-b36c03e4e771.
    # set x265_params $x265_params:hevc-aq

    set codec_extra $codec_extra -x265-params $x265_params
end

if string match -qr "x264|x265" -- $codec
    set codec_extra $codec_extra -preset $preset
end

# libx265 requires an even number for the height, which can fail if we're importing from some
# ad-hoc video format from the 90s or 00s.
set filters $filters "scale='iw:trunc(ow/a/2)*2'"

if test $old_codec_video = "mpeg4" && \
    # Assume blockiness is alleviated above 1500kbps
    test "$src_bitrate" -lt 1500

    # Ideally, we'd use nlmeans here, but ffmpeg's implementation is slower than encoding AV1!
    set filters $filters hqdn3d=3:2:2:1
else
    # Apply a deblocking filter anyway, but at such light levels that it can't hurt
    set filters $filters hqdn3d=1:1:0:0
end

# ffmpeg has conditional specifiers only for global streams, not for specific types.
# If we want to avoid an error on files without audio streams, we need to pre-detect their absence
if not has_audio $source
    set audio "-an"
else
    # here we can conditionally add any audio-related arguments we always want (when audio is present)
    set audio $audio -map_metadata:s:a 0:s:a
end

# Merge all filters into a single comma-delimited argument
set filter (printf "%s," $filters | string trim -c ,)

begin
    echo 'rule move'
    echo '    command = mv $in $out'

    echo 'rule ffencode'

    printf "    command = sleep \$delay; export AV_LOG_FORCE_COLOR=1; "

    # See https://trac.ffmpeg.org/ticket/6375 for `-max_muxing_queue_size 99999` reason
    if test $codec != "av1"
        printf "$FFMPEG -hide_banner -i \$in -write_id3v2 1 -id3v2_version 3 -max_muxing_queue_size 1024 -map_metadata:g 0:g -map_metadata:s:v 0:s:v -movflags use_metadata_tags -movflags faststart -ss \$start \$cmd_end -filter:v $filter -c:v $codec $codec_extra  -crf $crf $audio -export_xmp 1 -export_all 1 \$out ; "

    else
        # gdb -ex=r -ex=q --args
        set cmd_read_transform "$FFMPEG -hide_banner -i \$in -strict -1 -map 0:v -ss \$start \$cmd_end -filter:v $filter -pix_fmt yuvj420p -f yuv4mpegpipe pipe:1 "
        set av1_opts "--quantizer 100 --speed 5" # this is the default
        set cmd_encode "rav1e - $av1_opts --output -"
        set cmd_save "$FFMPEG -hide_banner -i pipe:0 -c copy -f mp4 \$out.av1"
        printf "env AV_LOG_FORCE_COLOR=1 $cmd_read_transform | $cmd_encode | $cmd_save && "

        if not string match -q -- "$audio" "-an"
           set audio_cmd "$FFMPEG -hide_banner -i \$in -ss \$start \$cmd_end $audio -map 0:a -f mp4 \$out.mp4a"
           printf "$audio_cmd && "

           set merge_cmd "$FFMPEG -hide_banner -i \$in -i \$out.av1 -i \$out.mp4a -map 1:v -map 2:a -map_metadata:g 0:g -map_metadata:s:a:0 0:s:a:0 -map_metadata:s:v:0 0:s:v -movflags faststart -movflags use_metadata_tags -c copy -f mp4 \$out";
           printf "$merge_cmd; "
        else
            set merge_cmd "$FFMPEG -hide_banner -i \$in -i \$out.av1 -map 1:v -an -map_metadata:g 0:g -map_metadata:s:v:0 0:s:v -movflags use_metadata_tags -movflags faststart -movflags faststart -movflags use_metadata_tags -c copy -f mp4 \$out";
            printf "$merge_cmd; "
        end
    end

    printf "echo 'segment'_complete; \n"

    printf '    description = $in from $start to $end\n' $crf

    echo 'rule ffconcat'
    echo '    command = ffconcat $in $out'
    echo '    pool = console'
end | tee -a $script > /dev/null

set length (ffprobe -v error -show_entries format=duration \
  -of default=noprint_wrappers=1:nokey=1 $source) # in seconds
echo "Length: $length"
if string match -qr "." range[1] && not string match -qr "." range[2]
    set range[2] (ts_to_secs $length)
end

if string match -qr "." $range[1] && string match -qr "." $range[2]
    set length (ts_to_secs (ts_diff $range[2] $range[1]))
    echo "Range: $range[1] to $range[2] ($length)"
else
    set range[1] 0
end

# Add a deterministic delay to prevent all threads starting at once
set start_delay 0
set delay_increment 100 # ms

set -l files ""
for index in (seq 1 $threads)
    set -l start (math -s4 "($length / $threads) * ($index - 1)")
    set -l end (math -s4 "($length / $threads) * $index")

    set -l start (math (ts_to_secs $range[1]) + $start)
    set -l end (math (ts_to_secs $range[1]) + $end)

    # echo "start: $start"
    # echo "end: $end"

    set -l cmd_end ""
    if test $index -ne $threads
        set cmd_end "-to $end"
    else if string match -qr "." $range[2]
        set cmd_end "-to $range[2]"
    end

    set -l outfile $outdir/$index.mp4

    begin
        echo "build $outfile: ffencode" (string replace -a ' ' '$ ' -- $source)
        echo "   start = $start"
        echo "   cmd_end = $cmd_end"
        echo "   end =" (if string match -qr . -- $end; echo $end; else; echo "end"; end)
        echo "   delay =" (math -s3 $start_delay/1000);
        # set start_delay (math $start_delay + $delay_increment)

        # Prefer index 2 and not 1 so it doesn't finish too early if the first/last chunk is too short
        if test $index -eq 2; or test $threads -eq 1
            # So we have something to look at during long-running jobs
            echo "    pool = console"
        end
    end | tee -a $script > /dev/null

    set files "$files $outfile"
end

begin
    printf 'build %s: ffconcat %s\n' (string replace -a ' ' '$ ' -- $dest) $files
    # printf 'build %s: move %s\n' (string replace -a ' ' '$ ' -- $real_dest) (string replace -a ' ' '$ ' -- $dest)
end | tee -a $script > /dev/null

printf "Splitting input into %d parts to reencode then merging.\n" $threads

# tmux new-session 'htop' \; split-window "cat $script; cat" \; split-window "ninja -C $outdir; cat; tmux kill-pane -a" \;
# NINJA_STATUS does not yet support ANSI color escapes. See https://github.com/ninja-build/ninja/issues/713
# set -l ninja_status '\\033[32m%e ([%f/%t])\\033[0m'
set -l ninja_status '[%f/%t] (%es): '
# env NINJA_STATUS=$ninja_status ninja -C $outdir

set start_time (date +%s)
set first_end_time
set last_end_time
set idx 1
set frames
set time
set fps
set bitrate
set qp

# Thanks to ninja's output pooling, we can guarantee we get all the output from
# one segment before we see any output from the next.
# nvim $outdir/build.ninja
env CLICOLOR_FORCE=1 ninja -C $outdir -v 2>&1 | tee /dev/tty | while read line

    if set segment_qp (string match -r "Avg QP: *([0-9.]+)" -- $line)
        set qp[$idx] $segment_qp[2]
    end

    if string match -qr "segment_complete" -- $line
        set last_end_time (date +%s)
        if test $idx -eq 1 # fish indices start at one
            set first_end_time $last_end_time
        end

        echo "Segment $idx completed in" (math -s 0 $last_end_time - $start_time) "seconds"
        set idx (math $idx + 1)
    end

end

if not test -f $dest || test (stat -c "%s" $dest) -eq 0
    # failure
    rm -f $dest
    rm -rf $outdir
    exit 1
end

function sum
    set sum 0
    for v in $argv
        set sum (math $sum + $v)
    end
    echo $sum
end

function avg
    if test (count $argv) -eq 0
        echo 0
    else
        echo (math (sum $argv)/(count $argv))
    end
end

set frames (ffprobe -select_streams v:0 -of default=noprint_wrappers=1:nokey=1 -show_entries stream=nb_frames -v error $dest)
set bitrate (ffprobe -select_streams v:0 -of default=noprint_wrappers=1:nokey=1 -show_entries stream=bit_rate -v error $dest)
set time (math $last_end_time - $start_time)
echo "*** " encoded $frames frames in {$time}s "("(math $frames / $time)" fps)", $bitrate kb/s, Avg QP: (math -s 2 (avg $qp)) " ***"

rm -rf $outdir

if type -q size_compare
    size_compare $source $dest
end

echo "done!"

